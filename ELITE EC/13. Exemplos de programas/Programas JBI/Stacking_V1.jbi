NOP
// ==============================================================================
// Stacking: pick parts from position P1 and place them at P2, in a stack 
// 12/2020
// ==============================================================================
// Align Gripper
IF 1=0 THEN
CALL JOB:Function_AlignGripper
PAUSE
ENDIF

// 1. Select reference coordinates system: CART=base frame, TOOL#=tool frame, USER#=customized user frame
CCOOD CART

// 2. On the top menu bar:{Monitor}-->{Variable}-->{Var P}, activate P010,P011,P020,P021,P025 (turn on the green light)
// 3. Teach P010 and P020: {Change}-->{Update Point}
// P010 - Pick Position | P020 - Place Position
SETJOINT P010 15.5057,-98.2996,130.6340,-121.1640,91.8781,-88.0583,0.0000,0.0000
SETJOINT P020 -0.3089,-76.1575,107.4895,-121.9726,89.2280,-105.3870,0.0000,0.0000

// 4. Set the stack configuartion
// Set the number of objects to stack up
SET LI010 5
// Set the gap distance between each object (Z offset)
SET LD011 50

// 5. Set the approach points LD010 and LD020:
// LD010 - Height of how much the object need to be lift up from the slot when pick up
SET LD010 100    
// LD020 - Height of how much the object need to be lift up from the slot when place down
SET LD020 100 


// Initializations 
SET LI001 0
SET LD001 0
SET LP011 P010
ADD LP011(2) LD010
DOUT OT#(1) OFF
PULSE OT#(2) T=0.5

LABEL *A
// Calculate offset distances 
SET LD001 LD011
MUL LD001 LI001
SET LP025 P020
ADD LP025(2) LD001
SET LP021 LP025
ADD LP021(2) LD020

// Move to pick position
MOVJ LP011 VJ=100% PL=7
MOVL P010 V=1000MM/S PL=0
// Close gripper
DOUT OT#(1) ON
TIMER T=0.25 S
MOVL LP011 V=1000MM/S PL=7

// Move to place position (slot in the stack)
MOVJ LP021 VJ=100% PL=7
MOVL LP025 V=1000MM/S PL=0
// Open gripper
DOUT OT#(1) OFF
PULSE OT#(2) T=0.5
MOVJ LP021 VJ=100% PL=7

// Increase LI001 by one after each stack up 
INC LI001            
JUMP *A IF LI001<LI010
END
